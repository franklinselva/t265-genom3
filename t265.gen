/*/
 * Copyright (c) 2019 LAAS/CNRS
 * All rights reserved.
 *
 * Redistribution  and  use  in  source  and binary  forms,  with  or  without
 * modification, are permitted provided that the following conditions are met:
 *
 *   1. Redistributions of  source  code must retain the  above copyright
 *      notice and this list of conditions.
 *   2. Redistributions in binary form must reproduce the above copyright
 *      notice and  this list of  conditions in the  documentation and/or
 *      other materials provided with the distribution.
 *
 * THE SOFTWARE  IS PROVIDED "AS IS"  AND THE AUTHOR  DISCLAIMS ALL WARRANTIES
 * WITH  REGARD   TO  THIS  SOFTWARE  INCLUDING  ALL   IMPLIED  WARRANTIES  OF
 * MERCHANTABILITY AND  FITNESS.  IN NO EVENT  SHALL THE AUTHOR  BE LIABLE FOR
 * ANY  SPECIAL, DIRECT,  INDIRECT, OR  CONSEQUENTIAL DAMAGES  OR  ANY DAMAGES
 * WHATSOEVER  RESULTING FROM  LOSS OF  USE, DATA  OR PROFITS,  WHETHER  IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR  OTHER TORTIOUS ACTION, ARISING OUT OF OR
 * IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 *
 *                                              Martin Jacquet - February 2020
 */
/* ---- Includes ---------------------------------------------------------- */
#pragma require "openrobots2-idl >= 2.0"
#pragma require "vision-idl"

// #include "or/pose/pose_estimator.gen"
#include "or/sensor/camera.gen"

/* ---- Component declaration --------------------------------------------- */
component t265 {
    version	    "0.1";
    email		"martin.jacquet@laas.fr";
    lang		"c";
    require	    "genom3 >= 2.99.26";
    codels-require	"realsense2", "opencv";

    /* ---- Interfaces ---------------------------------------------------- */
    provides or_camera;

    /* ---- Exceptions ---------------------------------------------------- */
    exception e_rs { string<128> what; };
    exception e_io { string<128> what; };
    exception e_mem { string<128> what; };

    /* ---- Ports --------------------------------------------------------- */
    /* interfaces ports:
     *  port out  or::sensor::frame         frame;
     *  port out  or::sensor::intrinsics    intrinsics;
     *  port out  or::sensor::extrinsics    extrinsics;
     */

    /* ---- Types --------------------------------------------------------- */
    native undist_s;
    /* ---- IDS ----------------------------------------------------------- */
    ids {
        or_camera::info info;
        or_camera::pipe pipe;

        unsigned short cam_id;
        undist_s undist;
    };

    /* ---- Tasks --------------------------------------------------------- */
    task main {
        codel<start> t265_main_start(out ::ids, out extrinsics)
            yield poll;

        async codel<poll> t265_main_poll(in info.started, inout pipe)
            yield pause::poll, pub;

        codel<pub> t265_main_pub(in pipe, in cam_id, in undist, out frame)
            yield poll;
    };

    /* ---- Hardware connection ------------------------------------------- */
    activity connect(in string<12> serial = : "serial number of the desired T265",
                     in unsigned short id = 1 : "id of desired camera (1: left, 2: right)",
                     in unsigned short size = 500 : "pixel size of the desired undistorted image",
                     in float fov = 2 : "Field of view of the undistorted image in radian") {
        task main;
        throw e_rs, e_io;

        codel<start> t265_connect(in serial, in id, in size, in fov, out cam_id, out pipe, out info.started, out intrinsics, out undist)
            yield ether;
    };

    activity disconnect() {
        task main;
        throw e_rs;

        codel<start> t265_disconnect(out pipe, out info.started)
            yield ether;
    };

    /* ---- Calibration --------------------------------------------------- */
    activity set_extrinsics(in sequence<float,6> ext_values) {
        task main;

        codel<start> t265_set_extrinsics(in ext_values, out extrinsics)
            yield ether;
    };

};
